AC_PREREQ(2.13)
AC_INIT(libgnomecanvas)

AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(libgnomecanvas, 1.112.1)

AM_MAINTAINER_MODE

AC_ISC_POSIX
AC_PROG_CC
AC_STDC_HEADERS
AM_PROG_LIBTOOL
AC_PROG_YACC
AC_PATH_PROGS(PATH_TO_XRDB, "xrdb")
AC_PATH_PROG(LIBGLADE_CONVERT, libglade-convert)
if test -z "$LIBGLADE_CONVERT" ; then
	AC_MSG_WARN([libglade-convert (from libglade 2.0) is needed for the checks in the tests directory.])
fi

GNOME_COMPILE_WARNINGS

GETTEXT_PACKAGE=libgnomecanvas-2.0
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE")

ALL_LINGUAS="az de es et fi fr it ja ko ms nl nn no pl pt ru sk sl sv ta tr uk wa zh_TW"
AM_GLIB_GNU_GETTEXT

AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)

dnl
dnl GTK+ check
dnl
CANVAS_MODULES="gtk+-2.0 >= 1.3.15 libart-2.0 >= 2.3.8 pango >= 0.25 pangoft2 >= 0.25"
PKG_CHECK_MODULES(LIBGNOMECANVAS, $CANVAS_MODULES)
AC_SUBST(LIBGNOMECANVAS_CFLAGS)
AC_SUBST(LIBGNOMECANVAS_LIBS)

PKG_CHECK_MODULES(CANVAS_GLADE, $CANVAS_MODULES libglade-2.0 >= 1.99.7)
AC_SUBST(CANVAS_GLADE_CFLAGS)
AC_SUBST(CANVAS_GLADE_LIBS)

dnl Don't use AC_PROG_AWK since we need the full pathname.
AC_PATH_PROGS(AWK, mawk gawk nawk awk, )
AC_PATH_PROGS(PERL, perl5 perl)

# glib-genmarshal
AC_PATH_PROG(GLIB_GENMARSHAL, glib-genmarshal)

AC_ARG_WITH(html-dir, [  --with-html-dir=PATH path to installed docs ])

if test "x$with_html_dir" = "x" ; then
  HTML_DIR='${datadir}/gtk-doc/html'
else
  HTML_DIR=$with_html_dir
fi

AC_SUBST(HTML_DIR)
GNOME_GTKDOC_CHECK

# define a MAINT-like variable REBUILD which is set if Perl
# and awk are found, so autogenerated sources can be rebuilt
AC_ARG_ENABLE(rebuilds, [  --disable-rebuilds      disable all source autogeneration rules],,enable_rebuilds=yes)
REBUILD=\#
if test "x$enable_rebuilds" = "xyes" && \
     test -n "$PERL" && \
     $PERL -e 'exit !($] >= 5.002)' > /dev/null 2>&1 && \
     test -n "$AWK" ; then
  REBUILD=
fi
AC_SUBST(REBUILD)

AC_OUTPUT([
Makefile
po/Makefile.in
libgnomecanvas/Makefile
libgnomecanvas/libgnomecanvas-2.0.pc
glade/Makefile
demos/Makefile
tests/Makefile
docs/Makefile
docs/reference/Makefile
])

cat $srcdir/message-of-doom
